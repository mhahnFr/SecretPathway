plugins {
    id 'java'
}

group 'mhahnFr'
version '1.0.0'

configurations {
    jutils
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
}

tasks.register('deploy', Exec) {
    dependsOn(jar)

    commandLine 'jpackage', '--input', 'build/libs',
                            '--name', project.name,
                            '--main-jar', project.name + '-' + version + '.jar',
                            '--copyright', 'Copyright Â© 2022 - 2023 mhahnFr',
                            '--app-version', version,
                            '--description', 'A MUD client',
                            '--vendor', project.group,
                            '--about-url', 'https://github.com/mhahnFr/SecretPathway',
                            '--license-file', 'LICENSE'
}

jar {
    dependsOn configurations.jutils
    manifest {
        attributes 'Main-Class': 'mhahnFr.SecretPathway.SecretPathway'
    }
    from {
        configurations.jutils.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    implementation project(':JUtilities')
    jutils project(':JUtilities')
}